#!/bin/csh

### Checks Portmaster log for moved ports and changes
### their origin either using portmaster or pkg.

## INIT section (don't change)

if ! { fuser portmaster.log >& /dev/null } then
	echo "Portmaster logfile currently in use, exiting!"	> /dev/stderr
	echo "(did you run refresh recently?)"			> /dev/stderr
	exit 1;
endif

if ("`grep moved portmaster.log`" == "") then
	echo "No moved ports to process, exiting!"		> /dev/stderr
	exit 1
endif

set pm=0

if ($1 == "pkg") then
	set pkg=1
else
	set pkg=0
	if ! { pkg info -e portmaster } then
	   echo "ports-mgmt/portmaster not installed (required)" > /dev/stderr
	   exit 1;
	endif
endif

## MAIN section

foreach a ("`grep moved portmaster.log`")
	set old=`echo "$a" | cut -d ' ' -f3`;
	set old_pm=`echo $old | cut -d '/' -f2`;
	set new=`echo "$a" | cut -d ' ' -f7`;

	if ($pkg == 1) then
		if { pkg set -yo $old\:$new; } then
			echo "Changed origin: $old => $new";
		else
			echo "Something went wrong.."		> /dev/stderr
			exit 1
		endif
	else
		if { portmaster -o $new $old_pm } then
			echo "Port moved: $old => $new";
		else
			echo "Something went wrong.."		> /dev/stderr
			exit 1
		endif
	endif
end

echo "Finished changing port origin(s), now refreshing port updates."
./refresh full

