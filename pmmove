#!/bin/csh

### Checks Portmaster log for moved ports and changes
### their origin either using portmaster or pkg.

## INIT section (don't change)

if ("`grep moved portmaster.log`" == "") then
	echo "No moved ports to process, exiting!"		> /dev/stderr
	exit 1
endif

set pm=0
echo >&! $0.log

if ($1 == "pkg") then
	set pkg=1
else
	set pkg=0

	if ! { pkg info -e portmaster } then
	   echo "ports-mgmt/portmaster not installed (required)" > /dev/stderr
	   exit 1;
	endif
endif

## MAIN section

foreach a ("`grep moved portmaster.log`")
	set old=`echo "$a" | cut -d ' ' -f3`;
	set old_pm=`echo $old | cut -d '/' -f2`;
	set new=`echo "$a" | cut -d ' ' -f7`;

	if ($pkg == 1) then
		pkg set -yo $old\:$new
		if ($? == 0) then
			echo "Changed origin: $old => $new";
		else
			echo "Something went wrong."		> /dev/stderr
			exit 1
		endif
	else
		portmaster -o $new $old_pm |& tee -a $0.log
		if ($? == 0) then
			echo "Port moved: $old => $new" | tee -a $0.log
		else
			echo "Something went wrong, see $0.log." > /dev/stderr
			exit 1
		endif
	endif
end

echo "Finished processing moved port(s), now refreshing port updates."
./refresh full

